plugins {
    id 'java'
}

apply from: "${project.rootDir}/proto.gradle"

description = 'cln grpc client package'

dependencies {
    api "io.grpc:grpc-protobuf:${grpcVersion}"
    api "io.grpc:grpc-stub:${grpcVersion}"

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

def protoOriginalTarget = layout.projectDirectory.dir("src/main/resources/cln/cln-grpc/proto")
def protoProcessedTarget = layout.projectDirectory.dir("src/main/java/org/tbk/lightning/cln/grpc/client")

def clnProtoFiles = [
        'node.proto',
        'primitives.proto'
]

task prepareDirs {
    mustRunAfter clean
    outputs.upToDateWhen { protoOriginalTarget.asFile.exists() && protoProcessedTarget.asFile.exists() }

    doLast {
        mkdir("${protoOriginalTarget.asFile}")
        mkdir("${protoProcessedTarget.asFile}")
    }
}

task fetchClnProtoFiles() {
    dependsOn prepareDirs

    outputs.upToDateWhen {
        clnProtoFiles
                .collect { protoOriginalTarget.file(it).asFile }
                .collect { it.exists() }
                .every()
    }

    def base = "https://raw.githubusercontent.com/ElementsProject/lightning/master/cln-grpc/proto"

    doLast {
        clnProtoFiles
                .collect { protoOriginalTarget.file(it).asFile }
                .findAll { !it.exists() }
                .each {
                    new URL("${base}/${it.name}").withInputStream { i -> it.withOutputStream { it << i } }
                }
    }
}

task processClnProtoFiles(type: Copy) {
    dependsOn prepareDirs, fetchClnProtoFiles, processResources

    from protoOriginalTarget
    into protoProcessedTarget
    filter(line -> line
            .replaceAll("package cln;", """
package cln;
option java_package = "org.tbk.lightning.cln.grpc.client";
option java_multiple_files = true;
            """)
            .replaceAll("import \"primitives.proto\";", "import \"org/tbk/lightning/cln/grpc/client/primitives.proto\";"))
}

generateProto.dependsOn(processClnProtoFiles)
