import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsTask
import io.franzbecker.gradle.lombok.task.DelombokTask
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    apply from: 'versions.gradle'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootGradlePluginVersion}")
        classpath("com.google.protobuf:protobuf-gradle-plugin:${protobufGradleVersion}")

        // some example projects use jmolecules which allows syntactic sugar via bytebuddy
        // bytebuddy plugin is disabled by default and must be manually enabled via
        // 1) `apply plugin: 'net.bytebuddy.byte-buddy-gradle-plugin'`
        // 2) specify bytebuddy plugin `byteBuddy { transformation { plugin = JMoleculesPlugin.class } }`
        // by submodules.
        classpath platform("org.jmolecules:jmolecules-bom:${jmoleculesBomVersion}")
        classpath 'org.jmolecules.integrations:jmolecules-bytebuddy'
    }
    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

plugins {
    id 'idea'
    id 'nebula.release' version '17.2.2'
    id 'nebula.lint' version '18.1.0'
    id 'com.netflix.nebula.project' version '10.1.5'
    id 'com.github.spotbugs' version '5.2.1' apply false
    id 'net.bytebuddy.byte-buddy-gradle-plugin' version "$bytebuddyPluginVersion" apply false
    id 'org.springframework.boot' version "$springBootGradlePluginVersion" apply false
    id 'io.franzbecker.gradle-lombok' version '5.0.0' apply false
    id 'org.graalvm.buildtools.native' version '0.9.28' apply false
}

contacts {
    'theborakompanioni+github@gmail.com' {
        moniker 'theborakompanioni'
        github 'theborakompanioni'
    }
}

def testcontainersRyukDisabled = System.getenv("TESTCONTAINERS_RYUK_DISABLED") ?: "true"
def isIdeEnv = System.getProperty('idea.active')

configurations.all {
    resolutionStrategy {
        // check for updates in modules every build
        cacheChangingModulesFor 0, 'seconds'
        preferProjectModules()
        // fail eagerly on version conflict (includes transitive dependencies)
        // e.g. multiple different versions of the same dependency (group and name are equal)
        failOnVersionConflict()
    }
}

allprojects {
    apply plugin: 'nebula.lint'

    group = 'io.github.theborakompanioni'

    gradleLint {
        alwaysRun = false // do not automatically run - must be executed manually
        autoLintAfterFailure = false // only run lint after a successful build
        criticalRules = [
                'dependency-parantheses',
                'overridden-dependency-version',
                'unused-exclude-by-conf',
                'dependency-tuple-expression'
        ]
        // duplicate dependency false positives from "compileOnly 'javax.servlet:javax.servlet-api'"
        excludedRules = ['duplicate-dependency-class']
    }
}

configure(rootProject) {
    defaultTasks 'clean', 'build', 'integrationTest'

    task clean {
        doLast {
            delete fileTree("${rootProject.projectDir}") {
                include '**/*.log'
                include '**/*.log.*.gz'
                include '**/*.log.*.tmp'
            }
        }
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'com.netflix.nebula.project'
    apply plugin: 'com.netflix.nebula.info'
    apply plugin: 'com.netflix.nebula.integtest-standalone'
    apply plugin: 'io.franzbecker.gradle-lombok'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
        maven {
            // needed for netlayer packages
            url "https://jitpack.io"
        }
        maven {
            // needed for consensusj
            url "https://gitlab.com/api/v4/projects/8482916/packages/maven"
        }
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.jmolecules:jmolecules-bom:${jmoleculesBomVersion}"
        }
    }

    dependencies {
        compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
        testCompileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
        spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:${findsecbugsPluginVersion}"

        implementation 'org.slf4j:slf4j-api'
        implementation "com.google.guava:guava:${guavaVersion}"

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    compileJava {
        options.compilerArgs << '-parameters'
    }

    jar {
        exclude('*-local.yml')
        exclude('**/mainnet/**')
        exclude('**/*.proto')

        includeEmptyDirs false
    }

    sourcesJar {
        // needed till duplicate bug in protobuf gradle plugin is resolved.
        // see https://github.com/google/protobuf-gradle-plugin/issues/522 (last checked 2023-01-18)
        duplicatesStrategy DuplicatesStrategy.EXCLUDE

        archiveClassifier.set('sources')
        from sourceSets.main.java
    }

    task testJar(type: Jar) {
        archiveClassifier.set('test')
        from sourceSets.test.output
    }

    pluginManager.withPlugin('org.springframework.boot') {

        dependencies {
            developmentOnly 'org.springframework.boot:spring-boot-devtools'
        }

        jar {
            enabled = true
        }

        bootJar {
            // creates a launch script for the executable jar
            launchScript()
            archiveClassifier.set('boot')

            exclude('*-local.yml')
            exclude('**/mainnet/**')
            exclude('**/*.proto')

            includeEmptyDirs false
            duplicatesStrategy DuplicatesStrategy.FAIL
        }

        bootRun {
            enabled = true
            args = ['--spring.profiles.active=development']

            environment "TESTCONTAINERS_RYUK_DISABLED", testcontainersRyukDisabled
        }

        springBoot {
            buildInfo {
                properties {
                    // overwrite "time" property to enable reproducible builds
                    // see https://reproducible-builds.org/docs/timestamps/
                    time = System.getenv('SOURCE_DATE_EPOCH') ?: null
                }
            }
        }
    }

    test {
        environment "TESTCONTAINERS_RYUK_DISABLED", testcontainersRyukDisabled

        finalizedBy jacocoTestReport // report is always generated after tests run

        useJUnitPlatform()
    }

    integrationTest {
        environment "TESTCONTAINERS_RYUK_DISABLED", testcontainersRyukDisabled

        useJUnitPlatform()
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report

        reports {
            xml.required = true
            html.required = true
            csv.required = false
        }
    }

    artifacts {
        archives testJar
    }

    lombok {
        version = "${lombokVersion}"
        sha256 = null // skip verifyLombok task - will be done by gradle dependency verification
    }

    tasks.withType(Test) {
        testLogging {
            // set options for log level LIFECYCLE
            events TestLogEvent.STARTED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED
            showExceptions true
            exceptionFormat TestExceptionFormat.FULL
            showCauses true
            showStackTraces true

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = '|  ', endItem = '  |'
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                }
            }
        }
    }

    task dependencyTree(type: DependencyReportTask) {}

    task delombok(type: DelombokTask, dependsOn: compileJava) {
        onlyIf { project.hasProperty('javadocEnabled') }
        def srcDirs = sourceSets.main.java.srcDirs.findAll { it.exists() }

        enabled = subprojects.isEmpty() && !srcDirs.isEmpty()

        ext.outputDir = layout.buildDirectory.dir("./delombok").get().asFile
        outputs.dir(outputDir)
        srcDirs.each {
            inputs.dir(it)
            args(it, "-d", outputDir)
        }

        doFirst {
            outputDir.deleteDir()
        }
    }

    javadoc {
        onlyIf { project.hasProperty('javadocEnabled') }
        dependsOn delombok
        source = delombok.outputDir
        failOnError = false
    }

    spotbugs {
        effort = Effort.MAX
        reportLevel = Confidence.LOW
        ignoreFailures = false
        excludeFilter = rootProject.file('./spotbugs-exclude.xml')
        reportsDir = rootProject.layout.buildDirectory.dir("./reports/spotbugs/$project.name").get().asFile
    }
    tasks.withType(SpotBugsTask) {
        onlyIf { gradle.taskGraph.hasTask('build') }
        reports {
            html.required = true
            xml.required = false
        }
    }

    checkstyle {
        toolVersion = "${checkstyleVersion}"
        configFile = rootProject.file('./checkstyle.xml')
        maxErrors = 0
        ignoreFailures = false
        reportsDir = rootProject.layout.buildDirectory.dir("./reports/checkstyle/$project.name").get().asFile
    }
    tasks.withType(Checkstyle) {
        onlyIf { gradle.taskGraph.hasTask('build') }
        reports {
            xml.required = false
            html.required = true
        }
    }

    tasks.withType(AbstractArchiveTask) {
        // activate reproducible archives
        // see https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    infoBroker {
        // exclude properties that prevent reproducible builds
        excludedManifestProperties = [
                'Created-By',
                'Built-By',
                'Build-Date',
                'Build-Date-UTC',
                'Build-Host',
                'Built-OS',
                'Build-Timezone',
                'Build-Url',
        ]
    }
}

// disable test tasks in check phase for all 'spring-testcontainer-*' subprojects.
// these subprojects tests are very expensive as they all start a lot of docker containers.
// you can invoke them manually via `./gradlew check -PtestcontainerTest`
configure(subprojects.findAll { project -> project.name.indexOf('spring-testcontainer-') == 0 }) {
    test.onlyIf { isIdeEnv || project.hasProperty('testcontainerTest') }
    integrationTest.onlyIf { isIdeEnv || project.hasProperty('testcontainerTest') }
}

// disable test tasks in check phase for all '*-example-application' subprojects.
// you can invoke them manually via `./gradlew check -PexampleTest`
configure(subprojects.findAll { project -> project.name.indexOf('-example-application') > 0 }) {
    test.onlyIf { isIdeEnv || project.hasProperty('exampleTest') }
    integrationTest.onlyIf { isIdeEnv || project.hasProperty('exampleTest') }
}

// disable test tasks in check phase for all 'bitcoin-fee-*' subprojects.
// these subprojects tests are very expensive as they all query external APIs.
// you can invoke them manually via `./gradlew check -PfeeProviderTest`
configure(subprojects.findAll { project -> project.name.indexOf('bitcoin-fee-') == 0 }) {
    test.onlyIf { isIdeEnv || project.hasProperty('feeProviderTest') }
    integrationTest.onlyIf { isIdeEnv || project.hasProperty('feeProviderTest') }
}

configure(subprojects.findAll { project -> project.subprojects.isEmpty() &&
        (project.name.startsWith('bitcoin-fee-'))
}) {
    apply from: "${project.rootDir}/proto.gradle"
}

// enable publishing for all subproject except for "example applications"
configure(subprojects.findAll { project -> project.subprojects.isEmpty() &&
        !project.pluginManager.hasPlugin('org.springframework.boot') &&
        project.name.indexOf('-example-application') < 0 }) {
    apply from: "${project.rootDir}/publish.gradle"
}


