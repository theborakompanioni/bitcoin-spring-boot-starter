apply plugin: 'com.google.protobuf'

sourceSets {
    main {
        proto {
            // In addition to the default 'src/main/proto'
            srcDir file('src/main/java')
        }
        java {
            srcDir layout.buildDirectory.dir('./generated/source/proto/main/java').get().asFile
            srcDir layout.buildDirectory.dir('./generated/source/proto/main/grpc').get().asFile
        }
    }
}
configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module("com.google.protobuf:protobuf-javalite") using module("com.google.protobuf:protobuf-java:${protobufVersion}")
        }
    }
}

dependencies {
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }

    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }

    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}
