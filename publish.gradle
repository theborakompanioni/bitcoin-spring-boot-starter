apply plugin: 'signing'
apply plugin: 'nebula.maven-publish'
apply plugin: 'nebula.publish-verification'

signing {
    // https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials
    sign publishing.publications.nebula
}

tasks.withType(Sign) {
    onlyIf { project.hasProperty('signing.password') }
}

publishing {
    publications {
        nebula(MavenPublication) { publication ->
            pom {
                packaging 'jar'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
            }
            sourcesJar
            javadocJar
        }
    }

    repositories {
        maven {
            def isReleaseVersion = rootProject.version.toString().matches('^([0-9]+)\\.([0-9]+)\\.([0-9]+)$')
            def releaseRepo = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotRepo = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : 'Unknown user'
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : 'Unknown password'
            }
        }
    }
}

